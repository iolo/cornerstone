plugins {
  id 'org.springframework.boot' version '2.7.0' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'io.freefair.lombok' version '6.4.3'
  id 'maven-publish'
  id 'java-library'
}

group 'com.day1co.cornerstone'
version '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
  mavenCentral()
  mavenLocal()
  maven {
    url = uri('https://maven.pkg.github.com/day1co/maven-repo')
    credentials {
        username = System.getenv('GITHUB_USERNAME')
        password = System.getenv('GITHUB_TOKEN')
    }
  }
}

dependencyManagement {
  imports {
    mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    mavenBom 'com.google.cloud:libraries-bom:25.1.0'
  }
}

dependencies {
  implementation 'com.day1co.cornerstone:cornerstone-spring-boot-autoconfigure:0.0.1-SNAPSHOT'
  implementation 'com.day1co.cornerstone:cornerstone-commons:0.0.1-SNAPSHOT'
}

tasks.named('test') {
  useJUnitPlatform()
}

java {
    //withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/day1co/maven-repo')
            credentials {
                username = System.getenv('GITHUB_USERNAME')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
    publications {
        githubPackages(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}
